extern {
    import battlecode.common.*;
}

use Common::*;
use RobotPlayer::rc;

// What number archon this is, i.e. how many archons go before it each turn
let archonOrder = 0;
let archonCount = 0;
let START_MINERS: [i32] = [3, 5, 6, 7];

fn preTurn() throws GameActionException {

}

fn spawnMiner() throws GameActionException {
    let dir = Miner::findSpawnSpace();
    if dir != null {
        Mem::addMinerCount();
        rc.buildRobot(RobotType::MINER, dir);
    }
}

fn spawnBuilder() throws GameActionException {
    let best: Direction = null;
    let best_lead = 1000;
    for dir in ALL_MOVE_DIRECTIONS {
        if rc.canBuildRobot(RobotType::BUILDER, dir) {
            let lead = rc.senseLead(rc.getLocation().add(dir));
            if lead < best_lead {
                best = dir;
                best_lead = lead;
            }
        }
    }
    if best != null {
        rc.buildRobot(RobotType::BUILDER, best);
    }
}

fn spawnSoldier() throws GameActionException {
    let dir = Combat::findSpawnSpace(RobotType::SOLDIER);
    if dir != null {
        Mem::addSoldierCount();
        rc.buildRobot(RobotType::SOLDIER, dir);
    }
}

fn spawnSage() throws GameActionException {
    let dir = Combat::findSpawnSpace(RobotType::SAGE);
    if dir != null {
        rc.buildRobot(RobotType::SAGE, dir);
    }
}

fn turn() throws GameActionException {
    if archonCount != rc.getArchonCount() {
        archonCount = rc.getArchonCount();
        archonOrder = rc.readSharedArray(0);
        if archonOrder == rc.getArchonCount() - 1 {
            rc.writeSharedArray(0, 0);
        } else {
            rc.writeSharedArray(0, archonOrder + 1);
        }
        rc.writeSharedArray(Mem::FRIENDLY_ARCHON_LOC + archonOrder, Mem::encodeLoc(rc.getLocation()));
    }

    if rc.getRoundNum() == 1 {
        spawnMiner();
    } else {
        if archonOrder == 0 {
            Mem::maintainMinerCorners();
            Mem::maintainSoldiers();
        }
        Mem::buildInfo.read();

        let minerCount = Mem::getMinerCount();
        let soldierCount = Mem::getSoldierCount();

        // Don't let one Archon dominate
        // They take turns spawning, or they can override the order if there's enough for each other archon to spawn a soldier to defend itself
        if rc.getRoundNum() % rc.getArchonCount() == archonOrder
            || rc.getTeamLeadAmount(rc.getTeam()) >= RobotType::MINER.buildCostLead * (rc.getArchonCount() - archonOrder) {
            let nLead = rc.senseNearbyLocationsWithLead(rc.getType().visionRadiusSquared, 1).len();
            let targetMinerCount = 6 + (rc.getMapWidth() + rc.getMapHeight())/10;
            let startMinerCount = START_MINERS[archonCount];
            if minerCount < targetMinerCount && (minerCount < startMinerCount || minerCount < soldierCount) {
                spawnMiner();
            } else if nLead < 10 && Mem::nearbyEnemies.len() == 0 {
                // This builder will suicide
                spawnBuilder();
            } else {
                spawnSoldier();
            }
        }

        if rc.isActionReady() {
            // Try to repair a robot in range.
            // The targeting logic picks the most important and vulnerable unit,
            // so it works pretty well in this case too!
            let bestUnit: RobotInfo = null;
            for i in Mem::nearbyAllies {
                if i.health < i.type.health && rc.canRepair(i.location) {
                    if Combat::unitBetter(bestUnit, i) {
                        bestUnit = i;
                    }
                }
            }
            if bestUnit != null {
                rc.repair(bestUnit.location);
            }
        }

        if archonOrder == archonCount - 1 {
            Mem::clearMinerCount();
            Mem::clearSoldierCount();
        }
        Mem::buildInfo.writeBack();
    }
}