extern {
    import battlecode.common.*;
}

use Common::*;
use RobotPlayer::rc;

// Copied from Mem since we want to inline them
define ENEMY_SOLDIER_LOC = 15;
define ENEMY_SOLDIER_LEN = 16;

fn preTurn() throws GameActionException {

}

class LocInfo {
    let loc: MapLocation;
    let enemyDamage: i32;
    let rubble: i32;
    let repairTarget: RobotInfo;
    let upgradeTarget: RobotInfo;
    let repairValue: i32;

    fn init(loc: MapLocation) throws GameActionException {
        self.loc = loc;
        self.rubble = rc.senseRubble(loc);
        self.enemyDamage = 0;
        self.repairTarget = null;
        self.upgradeTarget = null;
        self.repairValue = 0;
    }
}
fn locBetter(old: LocInfo, new: LocInfo): bool {
    if old == null { return true; }
    // TODO change the way this works so we spawn watchtowers close to the front line
    if new.enemyDamage < old.enemyDamage { return true; }
    if new.enemyDamage > old.enemyDamage { return false; }
    if new.rubble <= old.rubble + 20 {
        if new.repairValue > old.repairValue { return true; }
        if new.repairValue < old.repairValue { return false; }
    }
    new.rubble < old.rubble
}

let cloc: LocInfo = null;
let existsDamage = false;
fn microLoc(include_center: bool, include_others: bool): LocInfo throws GameActionException {
    let at = rc.getLocation();
    Combat::at = at;
    existsDamage = false;
    let n: LocInfo = null;
    towerUpgrade = null;
    let towerRubble = 10000;
    let locs: [LocInfo; 9] = [n,n,n,n,n,n,n,n,n];
    cloc = null;
    let directions = allDirections();
    for i in unroll 0..9 {
        let dir = directions[i];
        let l = at.add(dir);
        if (include_center && dir == Direction::CENTER) || (include_others && !rc.canSenseRobotAtLocation(l) && rc.onTheMap(l)) {
            locs[i] = LocInfo();
            locs[i].init(l);
            if dir == Direction::CENTER {
                cloc = locs[i];
            }
        }
    }
    for i in unroll ENEMY_SOLDIER_LOC..ENEMY_SOLDIER_LOC+ENEMY_SOLDIER_LEN {
        let r = rc.readSharedArray(i);
        if r != 0 {
            let loc = Mem::decodeLoc(r - 1);
            if !loc.isWithinDistanceSquared(at, 20) {
                for l in locs {
                    // Assume enemy soldiers we can't see could have moved 1 tile by now
                    if l != null && loc.isWithinDistanceSquared(l.loc, 20) {
                        l.enemyDamage += 10;
                        existsDamage = true;
                    }

                }
            }
        }
    }
    for i in Mem::nearbyEnemies {
        if i.type.damage > 0 {
            existsDamage = true;
            if i.mode.canAct {
                for l in locs {
                    if l != null {
                        // Assume they could move to attack us next turn, but give it a 2/3 chance
                        let d = i.location.distanceSquaredTo(l.loc);
                        if d <= max(20, i.type.actionRadiusSquared) {
                            if d <= i.type.actionRadiusSquared {
                                let rubble = rc.senseRubble(i.location);
                                l.enemyDamage += (i.type.getDamage(i.level) * 1000 / i.type.actionCooldown) / (10 + rubble);
                            } else {
                                let rubble = rc.senseRubble(i.location.add(i.location.dirTo(l.loc)));
                                l.enemyDamage += (i.type.getDamage(i.level) * 1000 / i.type.actionCooldown) / (10 + rubble) * 2 / 3;
                            }
                        }
                    }
                }
            }
        }
    }
    for bot in Mem::nearbyAllies {
        if bot.type.isBuilding() {
            for l in locs {
                if l != null {
                    if bot.location.isWithinDistanceSquared(l.loc, RobotType::BUILDER.actionRadiusSquared) {
                        let v = Combat::repairValue(2, bot);
                        if v > l.repairValue {
                            l.repairTarget = bot;
                            l.repairValue = v;
                        }
                        if (bot.type == RobotType::WATCHTOWER || (bot.type == RobotType::LABORATORY && Mem::buildInfo.shouldUpgradeLab)) && bot.mode == RobotMode::TURRET && bot.level == 1 {
                            let rubble = rc.senseRubble(bot.location);
                            if rubble < towerRubble {
                                l.upgradeTarget = bot;
                                towerRubble = rubble;
                            }
                        }
                    }
                }
            }
        }
    }

    let best = cloc;
    for loc in locs {
        if loc != null {
            if locBetter(best, loc) {
                best = loc;
            }
        }
    }
    best
}

fn findSpawnSpace(): Direction throws GameActionException {
    let best = microLoc(false, true);

    if best != null {
        rc.getLocation().dirTo(best.loc)
    } else {
        let n: Direction = null;
        n
    }
}

let towerUpgrade: RobotInfo = null;
let bestRepair: RobotInfo = null;
fn microMove(): bool throws GameActionException {
    let best = microLoc(true, rc.isMovementReady());
    let at = rc.getLocation();

    if rc.isMovementReady() {
        if best != null && (Mem::nearbyEnemies.len() > 0 || best.repairValue > 0) {
            if best.loc != at {
                rc.setIndicatorString("MICRO MOVE: damage: " + best.enemyDamage + ", repair?: " + (best.repairTarget != null) + " v " + best.repairValue);
                rc.setIndicatorLine(at, best.loc, 0, 0, 255);
                rc.move(at.dirTo(best.loc));
            } else {
                rc.setIndicatorString("MICRO STAY: damage: " + best.enemyDamage + ", repair?: " + (best.repairTarget != null) + " v " + best.repairValue);
            }
            bestRepair = best.repairTarget;
            towerUpgrade = best.upgradeTarget;
            true
        } else {
            rc.setIndicatorString("NO MICRO: damage: " + cloc.enemyDamage + ", repair?: " + (cloc.repairTarget != null) + " v " + cloc.repairValue);
            bestRepair = cloc.repairTarget;
            towerUpgrade = cloc.upgradeTarget;
            false
        }
    } else {
        bestRepair = cloc.repairTarget;
        towerUpgrade = cloc.upgradeTarget;
        false
    }
}

fn turn() throws GameActionException {
    Mem::buildInfo.read();

    let loc = rc.getLocation();

    let canMove = !microMove();
    if bestRepair != null {
        if rc.canRepair(bestRepair.location) {
            rc.repair(bestRepair.location);
        }
    }

    if Mem::closestArchon != null && rc.canSenseLocation(Mem::closestArchon) {
        let robot = rc.senseRobotAtLocation(Mem::closestArchon);
        if robot != null && robot.health < robot.type.getMaxHealth(robot.level) {
            if rc.isMovementReady() && !robot.location.isWithinDistanceSquared(loc, RobotType::BUILDER.actionRadiusSquared) {
                Paths::target = robot.location;
                Paths::targetMove(false);
                return;
            }
        }
    }

    if bestRepair == null {
        if Mem::buildInfo.shouldBuildLab && Mem::nearbyEnemies.len() == 0 {
            let nearbyAllies = Mem::nearbyAllies.len();
            if Mem::nearbyAllies.len() < 10 {
                let best: Direction = null;
                let bestRubble = 10000;
                for dir in ALL_MOVE_DIRECTIONS {
                    if rc.canBuildRobot(RobotType::LABORATORY, dir) {
                        let rubble = rc.senseRubble(loc.add(dir));
                        if rubble < bestRubble {
                            bestRubble = rubble;
                            best = dir;
                        }
                    }
                }
                if best != null {
                    rc.buildRobot(RobotType::LABORATORY, best);
                    Mem::buildInfo.builtLab();
                    Mem::spend(RobotType::LABORATORY.buildCostLead);
                    Mem::addLabCount();
                }
            }
        } else if towerUpgrade != null && towerUpgrade.type == RobotType::LABORATORY && rc.getTeamLeadAmount(rc.getTeam()) >= RobotType::LABORATORY.buildCostLead && rc.canMutate(towerUpgrade.location) {
            rc.mutate(towerUpgrade.location);
            Mem::spend(towerUpgrade.type.getLeadMutateCost(towerUpgrade.level + 1));
            Mem::buildInfo.upgradedLab();
        } else if bytecodeLeft() > 3000 && existsDamage && Mem::buildInfo.shouldBuildWatchtower && rc.getTeamLeadAmount(rc.getTeam()) >= RobotType::WATCHTOWER.buildCostLead {
            if towerUpgrade != null && rc.canMutate(towerUpgrade.location) {
                rc.mutate(towerUpgrade.location);
                Mem::spend(towerUpgrade.type.getLeadMutateCost(towerUpgrade.level + 1));
                if towerUpgrade.type == RobotType::LABORATORY {
                    Mem::buildInfo.upgradedLab();
                }
            } else {
                let spot = Combat::microLoc(RobotType::WATCHTOWER, 1, true, true, RobotType::WATCHTOWER.health, false, false);
                if spot != null {
                    let dir = rc.getLocation().dirTo(spot.loc);
                    if spot.rubble < 40 && rc.canBuildRobot(RobotType::WATCHTOWER, dir) {
                        rc.buildRobot(RobotType::WATCHTOWER, dir);
                        Mem::buildInfo.builtWatchtower();
                        Mem::spend(RobotType::WATCHTOWER.buildCostLead);
                    }
                }
            }
        }
        // else if !Mem::buildInfo.shouldBuildLab && !Mem::buildInfo.shouldBuildWatchtower && Mem::closestArchon != null && rc.getRoundNum() < 100 && loc.isWithinDistanceSquared(Mem::closestArchon, 8) {
        //     if Mem::closestArchon.isWithinDistanceSquared(loc, 13) {
        //         rc.setIndicatorString("SUICIDE MODE");
        //         // Sacrifice itself
        //         if rc.senseLead(loc) == 0 {
        //             rc.disintegrate();
        //         } else {
        //             for dir in ALL_MOVE_DIRECTIONS {
        //                 let l = loc.add(dir);
        //                 if Mem::closestArchon.isWithinDistanceSquared(l, 13) && rc.canMove(dir) && rc.senseLead(l) == 0 {
        //                     rc.move(dir);
        //                     return;
        //                 }
        //             }
        //         }
        //     }
        // }
    }

    if canMove {
        if !Mem::buildInfo.shouldBuildLab && Mem::buildInfo.shouldBuildWatchtower {
            let closestSoldier = Mem::getClosestSoldier(rc.getLocation());
            if closestSoldier != null {
                Paths::target = closestSoldier;
            } else if Mem::enemyArchons.len() > 0 {
                let closest: MapLocation = null;
                let closest_d = 100000;
                for i in Mem::enemyArchons {
                    let d = i.distanceSquaredTo(rc.getLocation());
                    if d < closest_d {
                        closest = i;
                        closest_d = d;
                    }
                }
                if closest != null {
                    Paths::target = closest;
                }
            }
        }
        else if Mem::buildInfo.shouldBuildLab {
            // if rc.getID() % 2 == 0 {
                let cornerX = if loc.x <= rc.getMapWidth() / 2 { 0 } else { rc.getMapWidth() - 1 };
                let cornerY = if loc.y <= rc.getMapHeight() / 2 { 0 } else { rc.getMapHeight() - 1 };
                let corner = MapLocation(cornerX, cornerY);
                if !rc.canSenseLocation(corner) {
                    Paths::target = corner;
                }
            // }
        }
        Paths::targetMove(true);
    }

    Mem::buildInfo.writeBack();
}